buildscript {
    ext {
        springBootVersion = '1.5.9.RELEASE'
    }
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.springframework:springloaded:1.2.8.RELEASE")
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: "idea"
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'com.monstar'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

jar {
    baseName = 'manstar-payment'
    version = '1.0.0'
}

idea {
    module {
        inheritOutputDirs = false
        outputDir = file("$buildDir/classes/main/")
        testOutputDir = file("$buildDir/classes/test/")
    }
}

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
}

configurations {
    mybatisGenerator
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-security")
    compile("org.mybatis.spring.boot:mybatis-spring-boot-starter:1.3.1")
    compile("mysql:mysql-connector-java")
    compile("org.springframework.boot:spring-boot-starter-data-redis")
    compile("org.springframework.session:spring-session-data-redis")
    compile("org.springframework.boot:spring-boot-autoconfigure")
    compile("org.springframework.boot:spring-boot-configuration-processor")

    compile("org.thymeleaf:thymeleaf:3.0.6.RELEASE")
    compile("org.thymeleaf:thymeleaf-spring4:3.0.6.RELEASE")
    compile("nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:2.2.2")
    compile("org.thymeleaf.extras:thymeleaf-extras-springsecurity4:3.0.2.RELEASE")

    compile("com.alibaba:fastjson:1.2.40")
    compile("joda-time:joda-time:2.9.9")
    compile("org.springframework.boot:spring-boot-starter-aop")
    compile("org.flywaydb:flyway-core")

    compile files("lib/alipay-sdk-java20170307171631.jar")
    compile files("lib/commons-logging-1.1.1.jar")

    compile('com.thoughtworks.xstream:xstream:1.4.10')
    compile('org.apache.commons:commons-lang3:3.7')
    compile('com.google.guava:guava:23.0')

    compile("org.apache.httpcomponents:httpclient:4.4") {
        exclude(module: "commons-logging")
    }

    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("org.springframework.boot:spring-boot-test-autoconfigure")
    testCompile("org.springframework.security:spring-security-test")

    mybatisGenerator 'org.mybatis.generator:mybatis-generator-core:1.3.5'
    mybatisGenerator 'mysql:mysql-connector-java:5.1.38'
    mybatisGenerator 'tk.mybatis:mapper:3.4.4'
}

sourceSets {
    def p = 'local'
    if (project.hasProperty("env")) {
        p = project.properties["env"]
    }
    main {
        resources {
            srcDir("conf/" + p)
        }
    }
}

def getDbProperties = {
    def properties = new Properties()
    file("src/main/resources/dbGenerate.properties").withInputStream { inputStream ->
        properties.load(inputStream)
    }
    properties
}

task mybatisGenerate {
    doLast {
        def properties = getDbProperties()
        ant.properties['targetProject'] = projectDir.path
        ant.properties['driverClass'] = properties.getProperty("jdbc.driverClassName")
        ant.properties['connectionURL'] = properties.getProperty("jdbc.url")
        ant.properties['userId'] = properties.getProperty("jdbc.user")
        ant.properties['password'] = properties.getProperty("jdbc.pass")
        ant.properties['modelPackage'] = properties.getProperty("modelPackage")
        ant.properties['mapperPackage'] = properties.getProperty("mapperPackage")
        ant.properties['sqlMapperPackage'] = properties.getProperty("sqlMapperPackage")
        ant.properties['src_main_java'] = sourceSets.main.java.srcDirs[0].path
        ant.properties['src_main_resources'] = sourceSets.main.resources.srcDirs[0].path

        ant.taskdef(
                name: 'mbgenerator',
                classname: 'org.mybatis.generator.ant.GeneratorAntTask',
                classpath: configurations.mybatisGenerator.asPath
        )
        ant.mbgenerator(overwrite: true,
                configfile: 'src/main/resources/generatorConfig.xml', verbose: true) {
            propertyset {
                propertyref(name: 'targetProject')
                propertyref(name: 'userId')
                propertyref(name: 'driverClass')
                propertyref(name: 'connectionURL')
                propertyref(name: 'password')
                propertyref(name: 'src_main_java')
                propertyref(name: 'src_main_resources')

                propertyref(name: 'modelPackage')
                propertyref(name: 'mapperPackage')
                propertyref(name: 'sqlMapperPackage')
            }
        }
    }
}